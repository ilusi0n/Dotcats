# $Id: PKGBUILD 60970 2011-12-19 21:33:58Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=dwm
pkgver=6.0
pkgrel=2
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama')
install=dwm.install
_source=(http://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
	dwm.desktop
	config.h
)
_patches=(
	00-dwm-6.0-buildflags.diff
    01-dwm-6.0-xft.diff
    02-dwm-6.0-pertag2.diff
    03-dwm-6.0-cycle.diff
    04-dwm-6.0-statuscolor_xft.diff
    05-dwm-6.0-focusurgent.diff
    06-dwm-6.0-netwmdemandsattention.diff
    07-dwm-6.0-xkeycodetokeysymfix.diff
    08-dwm-6.0-push.diff
    09-dwm-6.0-systray.diff
    10-dwm-6.0-togglemax.diff
    11-dwm-6.0-gaplessgrid.diff
    12-dwm-6.0-tilemovemouse.diff
    13-dwm-6.0-cflags.diff
    14-dwm-6.0-nplayout.diff
)
source=(${_source[@]} ${_patches[@]})

build() {
  cd $srcdir/$pkgname-$pkgver
  for PATCH in "${_patches[@]}"; do
  	echo "=> $PATCH"
    patch < ../$PATCH || return 1
  done
  cp $srcdir/config.h config.h
  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/^CFLAGS = -g/#CFLAGS += -g/g' config.mk
  sed -i 's/^#CFLAGS = -std/CFLAGS += -std/g' config.mk
  sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
  sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
  install -m644 -D $srcdir/dwm.desktop $pkgdir/usr/share/xsessions/dwm.desktop
}
