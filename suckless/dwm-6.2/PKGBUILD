# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=dwm
pkgver=6.2
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')
install=dwm.install
_source=(http://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
	dwm.desktop
	config.h
)

_patches=(
  00-buildflags-dwm-6.2.diff
  01-systray-dwm-6.2.diff
  02-bar-height-dwm-6.2.diff
  03-status2d-dwm-6.2.diff
  04-cyclelayouts-dwm-6.2.diff
  05-pergtag-functionalgaps-dwm-6.2.diff
  06-statusallmon-dwm-6.2.diff
  07-shiftviewclient-dwm-6.2.diff
  08-focusurgent-dwm-6.2.diff
  09-placemouse-dwm-6.2.diff
  10-cool-autostart-dwm-6.2.diff
  11-netclientliststacking-dwm-6.2.diff
  12-ewmhtags-dwm-6.2.diff
  13-decorhints-dwm-6.2.diff
  14-togglefullscreen-dwm-6.2.diff
  15-steam-dwm-6.2.diff
)
source=(${_source[@]} ${_patches[@]})

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  for PATCH in "${_patches[@]}"; do
  	echo "=> $PATCH"
    patch < ../$PATCH || return 1
  done
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}
